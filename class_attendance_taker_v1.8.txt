Option Explicit
' Created by Andrew Lam
' https://github.com/awhlam/online_attendance_taker
' Version 1.8 - 08/24/20

Function GetFolder() As String
    Dim fldr As FileDialog
    Dim sItem As String
    Set fldr = Application.FileDialog(msoFileDialogFolderPicker)
    With fldr
        .Title = "Select folder of text files to import"
        .AllowMultiSelect = False
        .InitialFileName = Application.DefaultFilePath
        If .Show <> -1 Then GoTo NextCode
        sItem = .SelectedItems(1)
    End With
NextCode:
    GetFolder = sItem
    Set fldr = Nothing
End Function

Sub attendance()
    
    Dim wbI As Workbook, wbO As Workbook
    Dim wsI As Worksheet
    Dim importFolderPath As String, deleteFilesWarning As String
    Dim fileName As Variant
    Dim lastDestinationRow As Long, lastSourceRow As Long
    Dim numFilesImported As Long, numFilesDeleted As Long

    Set wbI = ThisWorkbook
    Set wsI = wbI.Sheets("Imported Data")
    numFilesImported = 0
    numFilesDeleted = 0
    
    Application.ScreenUpdating = False
    
    'Clear previously imported data
    wsI.Columns(1).ClearContents
    wsI.Columns(2).ClearContents
    
    'Prompt the user to select a folder
    If wbI.Sheets("Start Here").deleteFilesCheckbox.Value = True Then
        deleteFilesWarning = "The files WILL BE DELETED after importing."
    Else
        deleteFilesWarning = "The files will NOT be deleted after importing."
    End If
    
    MsgBox ("Select the folder that contains the text files you want to import." & vbCrLf & "Folder contents will not be shown, just select the folder itself." & vbCrLf & vbCrLf & "Only files with the name format save-users-list-*.txt will be imported." & vbCrLf & deleteFilesWarning)
    
    importFolderPath = GetFolder()
    fileName = Dir(importFolderPath & "\save-users-list-*.txt")
    
    'Loop through text files in folder
    While fileName <> ""
        numFilesImported = numFilesImported + 1
        Set wbO = Workbooks.Open(importFolderPath & "\" & fileName)
        
        'Find the last row number of the destination and source sheets
        lastDestinationRow = wsI.Cells(Rows.Count, 1).End(xlUp).Row
        lastSourceRow = wbO.Sheets(1).Cells(Rows.Count, 1).End(xlUp).Row
        
        'If the last row is the not the first row, add one so that subsequent data will be pasted below it
        If lastDestinationRow <> 1 Then
            lastDestinationRow = lastDestinationRow + 1
        End If
        
        'Copy the data from the text file to the workbook
        wbO.Sheets(1).Range(Cells(1, 1), Cells(lastSourceRow, 1)).Copy wsI.Cells(lastDestinationRow, 1)
        wsI.Activate
        ActiveSheet.Range(Cells(lastDestinationRow, 2), Cells(lastDestinationRow + lastSourceRow - 1, 2)).Value = importFolderPath & "\" & fileName

        'Close the file without saving
        wbO.Close SaveChanges:=False
        
        'Delete the text file
        If wbI.Sheets("Start Here").deleteFilesCheckbox.Value = True Then
            Kill importFolderPath & "\" & fileName
            numFilesDeleted = numFilesDeleted + 1
        End If
        
        'Set the fileName to the next file
        fileName = Dir
    Wend
    
    'Show results to user
    Worksheets("Class Roster").Activate
    MsgBox (numFilesImported & " text files were copied to the 'Imported Data' worksheet." & vbCrLf & numFilesDeleted & " text files were deleted." & vbCrLf & "The 'Class Roster' worksheet has been updated.")
    Application.ScreenUpdating = True

End Sub
